translation("en", {
"job.milkdriver.letsgo"                             : "Let's go to Empire Bay Milk Co.",
"job.milkdriver.already."                           : "You're milkdriver already.",
"job.milkdriver.now"                                : "You're a milk truck driver now! Congratulations!",
"job.milkdriver.sitintruck"                         : "Sit into milk truck.",
"job.milkdriver.gotofarm"                           : "Let's go to farm",
"job.milkdriver.not"                                : "You're not a milk truck driver."
"job.milkdriver.needtruck"                          : "You need a milk truck."
"job.milkdriver.route.already"                      : "You got routes list already."
"job.milkdriver.route.now.loaded"                   : "You have route list now. Truck is loaded to %d / 120"
"job.milkdriver.route.now.empty"                    : "You have route list now. The truck is empty. Load milk to truck."
"job.milkdriver.notready"                           : "You don't have routes list."
"job.milkdriver.gofillingstation"                   : "Go to the milk filling station in Chinatown to load milk truck."
"job.milkdriver.driving"                            : "You're driving. Please stop the milk truck."
"job.milkdriver.milktruckloadedcarrymilk"           : "Milk truck is loaded to 120 / 120. Carry milk to addresses."
"job.milkdriver.truckalreadyloaded."                : "Milk truck already loaded."
"job.milkdriver.route.nohave"                       : "You don't have routes list."
"job.milkdriver.milknotenough"                      : "Milk is not enough. Go to the milk filling station in Chinatown to load milk truck."
"job.milkdriver.carrymilk"                          : "Carry milk to addresses."
"job.milkdriver.alreadybeenhere"                    : "You've already been here. Go to other address."
"job.milkdriver.nicejob.needpark"                   : "Nice job! Return the milk truck to milk filling station in Chinatown, park truck and take your money."
"job.milkdriver.unloadingcompleted.truckloaded"     : "Unloading completed. Milk truck is loaded to %d / 120. Go to next address."
"job.milkdriver.unloadingcompleted.milknotenough"   : "Unloading completed. Milk is not enough. Go to the milk filling station to load milk truck."
"job.milkdriver.carrymilkalladdresses"              : "Carry milk to all addresses."
"job.milkdriver.parkmilktruck."                     : "Go to the milk filling station to park the milk truck."
"job.milkdriver.completemilkdelivery."              : "Complete milk delivery to all addresses."
"job.milkdriver.nicejob"                            : "Nice job! You earned $20."
"job.milkdriver.route.title"                        : "========== List of route =========="
"job.milkdriver.route.completed"                    : "%d. %s - completed"
"job.milkdriver.route.waiting"                      : "%d. %s - waiting"
"job.milkdriver.milktruckloaded"                    : "Milk truck is loaded to %d / 120"

"job.milkdriver.help.title"                         :    "List of available commands for MILKDRIVER JOB:"
"job.milkdriver.help.job"                           :    "Get milkdriver job"
"job.milkdriver.help.jobleave"                      :    "Leave milkdriver job"
"job.milkdriver.help.ready"                         :    "Get route list"
"job.milkdriver.help.load"                          :    "Load milk into milk truck"
"job.milkdriver.help.unload"                        :    "Unload milk"
"job.milkdriver.help.park"                          :    "Park the milk truck to Empire Bay Milk Company parking"
"job.milkdriver.help.check"                         :    "Checking loading milk truck"
"job.milkdriver.help.list"                          :    "See list of route"

});

translation("ru", {
"job.milkdriver.letsgo"                             : "Отправляйтесь к зданию Empire Bay Milk Co.",
"job.milkdriver.already."                           : "Вы уже работаете развозчиком молока.",
"job.milkdriver.now"                                : "Вы стали развозчиком молока. Поздравляем!",
"job.milkdriver.sitintruck"                         : "Садитесь в молоковоз.",
"job.milkdriver.gotofarm"                           : "Отправляйтесь на ферму.",
"job.milkdriver.not"                                : "Вы не работаете развозчиком молока."
"job.milkdriver.needtruck"                          : "Вам нужен молоковоз."
"job.milkdriver.route.already"                      : "У вас уже есть маршрутный лист"
"job.milkdriver.route.now.loaded"                   : "Вы получили маршрутный лист. Молоковоз заполнен на %d из 120"
"job.milkdriver.route.now.empty"                    : "Вы получили маршрутный лист. Молоковоз пуст. Загрузите молоко."
"job.milkdriver.notready"                           : "У вас нет маршрутного листа."
"job.milkdriver.gofillingstation"                   : "Отправляйтесь к молочному заводу в Chinatown для загрузки молока."
"job.milkdriver.driving"                            : "Остановите молоковоз."
"job.milkdriver.milktruckloadedcarrymilk"           : "Молоковоз заполнен на 120 из 120. Развезите молоко по адресам."
"job.milkdriver.truckalreadyloaded."                : "Молоковоз уже загружен."
"job.milkdriver.route.nohave"                       : "У вас нет маршрутного листа."
"job.milkdriver.milknotenough"                      : "Молоко закончилось. Отправляйтесь к молочному заводу в Chinatown для загрузки молока."
"job.milkdriver.carrymilk"                          : "Развезите молоко по адресам."
"job.milkdriver.alreadybeenhere"                    : "Вы уже были здесь. Направляйтесь к следующему адресу доставки."
"job.milkdriver.nicejob.needpark"                   : "Отлично! Верните молоковоз обратно к молочному заводу в Chinatown, припаркуйте и получите Ваш заработок."
"job.milkdriver.unloadingcompleted.truckloaded"     : "Разгрузка прошла успешна. Молоковоз заполнен на %d из 120. Направляйтесь к следующему адресу доставки."
"job.milkdriver.unloadingcompleted.milknotenough"   : "Разгрузка прошла успешна. Молоко закончилось. Отправляйтесь к молочному заводу в Chinatown для загрузки молока."
"job.milkdriver.carrymilkalladdresses"              : "Развезите молоко по всем адресам."
"job.milkdriver.parkmilktruck."                     : "Чтобы припарковать молоковоз, отправляйтесь к молочному заводу в Chinatown."
"job.milkdriver.completemilkdelivery."              : "Завершите доставку молока по всем адресам."
"job.milkdriver.nicejob"                            : "Отличная работа! Вы заработали $20."
"job.milkdriver.route.title"                        : "========== Маршрутный лист =========="
"job.milkdriver.route.completed"                    : "%d. %s - завершено"
"job.milkdriver.route.waiting"                      : "%d. %s - ожидается"
"job.milkdriver.milktruckloaded"                    : "Молоковоз заполнен на %d из 120"

"job.milkdriver.help.title"                         :    "Список доступных команд для развозчика молока:"
"job.milkdriver.help.job"                           :    "Устроиться на работу развозчиком молока"
"job.milkdriver.help.jobleave"                      :    "Уволиться с работы"
"job.milkdriver.help.ready"                         :    "Получить маршрутный лист"
"job.milkdriver.help.load"                          :    "Загрузить молоко в молоковоз"
"job.milkdriver.help.unload"                        :    "Выгрузить молоко"
"job.milkdriver.help.park"                          :    "Припарковать молоковоз на парковке"
"job.milkdriver.help.check"                         :    "Проверить загруженность молоковоза"
"job.milkdriver.help.list"                          :    "Посмотреть маршрутный лист"

});

include("controllers/jobs/milkdriver/commands.nut");

local job_milk = {};
local milktrucks = {};

const RADIUS_JOB_MILK = 2.0;
const MILK_JOB_X =  176.834;
const MILK_JOB_Y = 439.305;
const MILK_JOB_Z = -20.1758;
const MILK_JOB_SKIN = 171;
const MILK_JOB_DISTANCE = 100;
const MILK_JOB_NUMBER_STATIONS = 6;

local milkname = [
    "Hill Of Tara Black Exit",                  // Hill Of Tara Black Exit
    "Empire Diner Kingston",                    // Empire Diner Kingston
    "Empire Diner Greenfield",                  // Empire Diner Greenfield
    "Empire Diner Highbrook"                    // Empire Diner Highbrook
];

// 788.288, -78.0801, -20.132   // coords of place to load milk truck
// 551.762, -266.866, -20.1644  // coords of place to get job milkdriver

local milkcoordsall = [
    [-1162.5,   1599.26,    6.46012,    "The Hill Of Tara (Emergency exit)" ],
    [-1594.78,  1602.47,    -5.90717,   "Empire Diner (Kingston)"           ],
    [-1418.26,  975.404,    -13.577,    "Empire Diner (Greenfield)"         ],
    [-634.34,   1292.31,    3.26542,    "Empire Diner (Highbrook)"          ],
    [131.316,   -434.314,   -19.9878,   "Empire Diner (Oyster Bay)"         ],
    [-758.831,  -377.673,   -20.6368,   "Illias Bar (SouthPort)"            ],
    [-561.424,  441.203,    1.07133,    "Stellas Diner (Uptown)"            ],
    [-626.332,  345.59,     1.49028,    "The Mona Lisa (Uptown)"            ],
    [-54.6979,  722.117,    -21.9211,   "Freddys Bar (Little Italy)"        ],
    [239.853,   696.646,    -24.0973,   "Stellas Diner (Little Italy)"      ],
    [624.514,   904.991,    -12.6217,   "The Dragstrip (North Millvlille)"  ],
    [-1396.19,  476.477,    -22.2102,   "The Lone Star (Hunters Point)"     ],
    [-1591.16,  187.75,     -12.8383,   "Empire Diner (Sand Island)"        ],
    [-1546.54, -171.372,    -19.8428,   "Steaks & Chops (Sand Island)"      ]
];


addEventHandlerEx("onServerStarted", function() {
    log("[jobs] loading milkdriver job...");
    // milktrucks[i][0] - Truck ready: true/false
    // milktrucks[i][1] - milk load: integer
    milktrucks[createVehicle(19, 172.868, 436, -20.04, -178.634, 0.392045, -0.829271)]  <- 0 ;
    milktrucks[createVehicle(19, 168.812, 436, -20.04, 179.681, 0.427494, -0.637235)]  <- 0 ;

    //creating 3dtext for bus depot
    create3DText ( MILK_JOB_X, MILK_JOB_Y, MILK_JOB_Z+0.35, "EMPIRE BAY MILK CO.", CL_ROYALBLUE );
    create3DText ( MILK_JOB_X, MILK_JOB_Y, MILK_JOB_Z+0.20, "/help job milk", CL_WHITE.applyAlpha(75), 3 );

    registerPersonalJobBlip("milkdriver", MILK_JOB_X, MILK_JOB_Y);
});

addEventHandlerEx("onPlayerConnect", function(playerid, name, ip, serial) {
     job_milk[playerid] <- {};
     job_milk[playerid]["milkready"] <- false;
     job_milk[playerid]["milkcoords"] <- [];
     job_milk[playerid]["milkstatus"] <- [false, false, false, false, false, false];
     job_milk[playerid]["milkcomplete"] <- 0;  // number of completed milk address. Default is 0
});


local milkJobStationMarks = {};
function createMilkJobStationMarks(playerid, data) {
    if (!(playerid in milkJobStationMarks)) {
        milkJobStationMarks[playerid] <- {};
    }

    // ignore creation if they already set
    if (milkJobStationMarks[playerid].len()) {
        return;
    }

    foreach (id, value in data) {
        milkJobStationMarks[playerid][id] <- {
            text1 = createPrivate3DText(playerid, value[0], value[1], value[2]+0.35, "=== PARK HERE ===", CL_RIPELEMON, MILK_JOB_DISTANCE ),
            text2 = createPrivate3DText(playerid, value[0], value[1], value[2]+0.20, "/milk unload", CL_WHITE.applyAlpha(150), 5 ),
            blip  = createPrivateBlip(playerid, value[0], value[1], ICON_RED, 4000.0 )
        };
    }
}

function removeMilkJobStationMark(playerid, id) {
    if (playerid in milkJobStationMarks) {
        if (id in milkJobStationMarks[playerid]) {
            remove3DText(milkJobStationMarks[playerid][id].text1);
            remove3DText(milkJobStationMarks[playerid][id].text2);
            removeBlip(milkJobStationMarks[playerid][id].blip);

            delete milkJobStationMarks[playerid][id];
        }
    }
}

function clearMilkJobStationMarks(playerid) {
    if (playerid in milkJobStationMarks) {
        foreach (id, value in milkJobStationMarks[playerid]) {
            remove3DText(milkJobStationMarks[playerid][id].text1);
            remove3DText(milkJobStationMarks[playerid][id].text2); // curenlty disabled
            removeBlip(milkJobStationMarks[playerid][id].blip);
        }
        delete milkJobStationMarks[playerid];
    }
}


/**
 * Check is player is a milk driver
 * @param  {int}  playerid
 * @return {Boolean} true/false
 */
function isMilkDriver(playerid) {
    return (isPlayerHaveValidJob(playerid, "milkdriver"));
}

/**
 * Check is player's vehicle is a milk truck
 * @param  {int}  playerid
 * @return {Boolean} true/false
 */
function isPlayerVehicleMilk(playerid) {
    return (isPlayerInValidVehicle(playerid, 19));
}


/**
 * Check is milkReady
 * @param  {int}  playerid
 * @return {Boolean} true/false
 */
function isMilkReady(playerid) {
    return job_milk[playerid]["milkready"];
}


// working good, check
function milkJob ( playerid ) {

    if(!isPlayerInValidPoint(playerid, MILK_JOB_X, MILK_JOB_Y, RADIUS_JOB_MILK)) {
        return msg( playerid, "job.milkdriver.letsgo" );
    }

    if(isMilkDriver(playerid)) {
        return msg( playerid, "job.milkdriver.already.");
    }

    if(isPlayerHaveJob(playerid)) {
        return msg(playerid, "job.alreadyhavejob", getLocalizedPlayerJob(playerid));
    }
    screenFadeinFadeoutEx(playerid, 250, 200, function() {
        msg( playerid, "job.milkdriver.now" );
        msg( playerid, "job.milkdriver.sitintruck" );

        players[playerid]["job"] = "milkdriver";

        players[playerid]["skin"] = MILK_JOB_SKIN;
        setPlayerModel( playerid, MILK_JOB_SKIN );

         // create private blip job
        createPersonalJobBlip( playerid, MILK_JOB_X, MILK_JOB_Y);
    });
}


// working good, check
function milkJobLeave ( playerid ) {

    if(!isPlayerInValidPoint(playerid, MILK_JOB_X, MILK_JOB_Y, RADIUS_JOB_MILK)) {
        return msg( playerid, "job.milkdriver.gotofarm" );
    }

    if(!isMilkDriver(playerid)) {
        return msg( playerid, "job.milkdriver.not");
    }
    screenFadeinFadeoutEx(playerid, 250, 200, function() {
        msg( playerid, "job.leave" );

        players[playerid]["job"] = null;

        players[playerid]["skin"] = players[playerid]["default_skin"];
        setPlayerModel( playerid, players[playerid]["default_skin"]);

        job_milk[playerid]["milkready"] = false;
        job_milk[playerid]["milkcoords"] = null;

        // remove private blip job
        removePersonalJobBlip ( playerid );

        // clear all marks
        clearMilkJobStationMarks(playerid);
    });
}


// working good, check
function milkJobReady ( playerid ) {

    if(!isMilkDriver(playerid)) {
        return msg( playerid, "job.milkdriver.not");
    }

    if (!isPlayerVehicleMilk(playerid)) {
        return msg(playerid, "job.milkdriver.needtruck");
    }

    if(isMilkReady(playerid)) {
        return msg( playerid, "job.milkdriver.route.already");
    }

    job_milk[playerid]["milkready"] = true;
    job_milk[playerid]["milkcoords"] = getRandomSubArray(milkcoordsall, MILK_JOB_NUMBER_STATIONS);
    dbg(job_milk[playerid]["milkcoords"]);

    // create milk marks
    createMilkJobStationMarks(playerid, job_milk[playerid]["milkcoords"]);

    local vehicleid = getPlayerVehicle(playerid);
    if(milktrucks[vehicleid] >= 12) {
        msg( playerid, "job.milkdriver.route.now.loaded", milktrucks[vehicleid]);
    } else {
        msg( playerid, "job.milkdriver.route.now.empty" );
    }
}

// working good, check
function milkJobLoad ( playerid ) {

    if(!isMilkDriver(playerid)) {
        return msg( playerid, "job.milkdriver.not");
    }

    if (!isPlayerVehicleMilk(playerid)) {
        return msg(playerid, "job.milkdriver.needtruck");
    }

    if (!isMilkReady(playerid)) {
        return msg( playerid, "job.milkdriver.notready" );
    }

    if(!isVehicleInValidPoint(playerid, 170.737, 436.886, 4.0)) {
        return msg( playerid, "job.milkdriver.gofillingstation" );
    }

    if(isPlayerVehicleMoving(playerid)){
        return msg( playerid, "job.milkdriver.driving");
    }

    local vehicleid = getPlayerVehicle(playerid);
// load in 1 pint = 0.5 litres
    if(milktrucks[vehicleid] < 120) {
        milktrucks[vehicleid] = 120;
        msg( playerid, "job.milkdriver.milktruckloadedcarrymilk" );
    } else {
        msg( playerid, "job.milkdriver.truckalreadyloaded." );
    }
}

// working good, check
function milkJobUnload ( playerid ) {

    if(!isMilkDriver(playerid)) {
        return msg( playerid, "job.milkdriver.not");
    }

    if (!isPlayerVehicleMilk(playerid)) {
        return msg(playerid, "job.milkdriver.needtruck");
    }

    if (!isMilkReady(playerid)) {
        return msg( playerid, "job.milkdriver.route.nohave" );
    }

    local vehicleid = getPlayerVehicle(playerid);
    if(milktrucks[vehicleid] < 12) {
        return msg( playerid, "job.milkdriver.milknotenough" );
    }

    local check = false;
    local i = -1;
    foreach (key, value in job_milk[playerid]["milkcoords"]) {
        if (isVehicleInValidPoint(playerid, value[0], value[1], 5.0 )) {
            check = true;
            i = key;
            break;
        }
    }

    if (!check) {
       return  msg( playerid, "job.milkdriver.carrymilk" );
    }

    if(isPlayerVehicleMoving(playerid)){
        return msg( playerid, "job.milkdriver.driving");
    }

    if(job_milk[playerid]["milkstatus"][i]) {
        return msg( playerid, "job.milkdriver.alreadybeenhere" );
    }

    local vehicleid = getPlayerVehicle(playerid);
    milktrucks[vehicleid] -= 12;
    job_milk[playerid]["milkstatus"][i] = true;
    job_milk[playerid]["milkcomplete"] += 1;

    // remove blip on complete unload
    removeMilkJobStationMark(playerid, i);

    if (job_milk[playerid]["milkcomplete"] == MILK_JOB_NUMBER_STATIONS) {
        msg( playerid, "job.milkdriver.nicejob.needpark" );
    } else {
        if (milktrucks[vehicleid] >= 12) {
            msg( playerid, "job.milkdriver.unloadingcompleted.truckloaded", milktrucks[vehicleid] );
        } else {
            msg( playerid, "job.milkdriver.unloadingcompleted.milknotenough" );
        }
    }
}


// working good, check
function milkJobPark ( playerid ) {

    if(!isMilkDriver(playerid)) {
        return msg( playerid, "job.milkdriver.not");
    }

    if (!isPlayerVehicleMilk(playerid)) {
        return msg(playerid, "job.milkdriver.needtruck");
    }

    if (!isMilkReady(playerid)) {
        return msg( playerid, "job.milkdriver.route.nohave" );
    }

    if (job_milk[playerid]["milkcomplete"] != MILK_JOB_NUMBER_STATIONS) {
        return msg( playerid, "job.milkdriver.carrymilkalladdresses" );
    }

    if(!isVehicleInValidPoint(playerid, 170.737, 436.886, 4.0)) {
        return msg( playerid, "job.milkdriver.parkmilktruck." );
    }

    if(isPlayerVehicleMoving(playerid)){
        return msg( playerid, "job.milkdriver.driving");
    }

    if (job_milk[playerid]["milkcomplete"] < MILK_JOB_NUMBER_STATIONS) {
        return msg( playerid, "job.milkdriver.completemilkdelivery." );
    }

    job_milk[playerid]["milkcomplete"] = 0;
    job_milk[playerid]["milkready"] = false;
    job_milk[playerid]["milkstatus"] <- [false, false, false, false, false, false];
    msg( playerid, "job.milkdriver.nicejob" );
    addMoneyToPlayer(playerid, 20);

    // clear all marks
    clearMilkJobStationMarks( playerid );
}


// working good, check
function milkJobList ( playerid ) {

    if(!isMilkDriver(playerid)) {
        return msg( playerid, "job.milkdriver.not");
    }

     if (!isMilkReady(playerid)) {
        return msg( playerid, "job.milkdriver.route.nohave" );
    }

    msg( playerid, "job.milkdriver.route.title", CL_JOB_LIST);
    foreach (key, value in job_milk[playerid]["milkstatus"]) {
        local i = key+1;
        if (value == true) {
            msg( playerid, "job.milkdriver.route.completed", [i, job_milk[playerid]["milkcoords"][key][3]], CL_JOB_LIST_GR);
        } else {
            msg( playerid, "job.milkdriver.route.waiting", [i, job_milk[playerid]["milkcoords"][key][3]], CL_JOB_LIST_R);
        }
    }
}

// working good, check
function milkJobCheck ( playerid ) {

    if(!isMilkDriver(playerid)) {
        return msg( playerid, "job.milkdriver.not");
    }

    if (!isPlayerVehicleMilk(playerid)) {
        return msg(playerid, "job.milkdriver.needtruck");
    }

    if (!isMilkReady(playerid)) {
        return msg( playerid, "job.milkdriver.route.nohave" );
    }

    local vehicleid = getPlayerVehicle(playerid);
    msg( playerid, "job.milkdriver.milktruckloaded", milktrucks[vehicleid] );
}
